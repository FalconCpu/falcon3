
HWREGS_BASE = 0xE0000000
HWREGS_UART_TX = 0x10
HWREGS_SIM = 0x44
HWREGS_LEDR = 0x04

# Entry point for the program
ld $31, 0x4000000    # Initialize stack pointer to top of memory
ld $29, 0x100        # initialize globals pointer to below the heap start
jsr /initializeMemorySystem()
# jsr /init          # Call the initialization function
jsr /main()            # Call the main function

# check to see if we are in a simulation
ld $1, HWREGS_BASE
ldw $2, $1[HWREGS_SIM]
beq $2, 0, .not_sim
ld $30,0             # In a simulation do a jump to address zero to exit the program
jmp $30[0]

.not_sim:
# flash LEDs to show finished
ld $2,0
.finished_loop:
stw $2, $1[HWREGS_LEDR]
ld $3, 10000000
.delay:
sub $3,1
bne $3, 0, .delay
xor $2, 0xFFF
jmp .finished_loop


# ==========================================================
#                     printInt
# ==========================================================
# $1 = value to print

/printInt:
    sub $31, 16                # Allocate 16 bytes of stack space
    ld $7, HWREGS_BASE         # Load base address of hardware registers
    bge $1, 0, .positive       # If value is positive, skip
    ld  $2, '-'                # Print the '-' character
    stw $2, $7[HWREGS_UART_TX]
    sub $1, 0, $1              # Negate the value

.positive:
    ld $6, $31                 # $6 = pointer to work space

.loop:
    modu $2, $1, 10            # $2 = $1 % 10
    divu $1, $1, 10            # $1 = $1 / 10
    add $2, '0'                # Convert digit to ASCII
    stb $2, $6[0]              # Store digit in work space
    add $6, 1                  # Increment work space pointer
    bne $1, 0, .loop           # Repeat until $1 is 0

.print:
    sub $6, 1                   # Decrement work space pointer
    ldb $2, $6[0]               # Load digit from work space
    stw $2, $7[HWREGS_UART_TX] # Print the digit
    bne $6, $31, .print         # Repeat until work space pointer is at base

.end:
    add $31, 16
    ret

# ==========================================================
#                     printString
# ==========================================================
# $1 = string to print

/printString:
    ld $7, HWREGS_BASE         # Load base address of hardware registers
    ldw $2, $1[-4]             # Load length of string
    add $2, $1                 # Pointer to just past the end of string
    jmp .cond

.loop:
    ldb $3, $1[0]               # Load character from string
    stw $3, $7[HWREGS_UART_TX]  # Print the character
    add $1, 1                   # Increment string pointer
.cond:
    bne $1, $2, .loop           # Repeat until string pointer is at end
    ret

# ==========================================================
#                     printChar
# ==========================================================
# $1 = character to print

/printChar:
    ld $7, HWREGS_BASE         # Load base address of hardware registers
    stw $1, $7[HWREGS_UART_TX] # Print the character
    ret

# ==========================================================
#                     strcmp
# ==========================================================
# Compare two strings lexicographically
# $1 = pointer to string 1
# $2 = pointer to string 2
# returns:
# $8 negative if string 1 < string 2, 0 if equal, positive if string 1 > string 2

/strcmp:
    ldw $3, $1[-4]       # get the length of string 1
    ldw $4, $2[-4]       # get the length of string 2

    ld $5, $3           # get the length of the shorter string
    blt $3, $4, .skip1
    ld $5, $4
.skip1:
    add $5, $5, $1       # point to end of shorter string
    jmp .check_end       # jump to compare point (in case both strings are empty)

.loop:
    ldb $8, $1[0]        # get a character from string 1
    ldb $6, $2[0]        # get a character from string 2
    sub $8, $6           # subtract the characters
    bne $8, 0, .done     # if they are not equal, we are done
    add $1, 1            # increment string 1 pointer
    add $2, 1            # increment string 2 pointer
.check_end:
    blt $1, $5, .loop    # if we have not reached the end of shorter string then loop

    sub $8, $3, $4       # set $8 indicate which string is longer
.done:
    ret


# ==========================================================
#                     strequal
# ==========================================================
# Compare two strings for equality
# $1 = pointer to string 1
# $2 = pointer to string 2
# returns:
# $8 = 1 if equal, 0 if not equal
#
# This makes use of the fact strings are word-aligned, and padded with 0 to a multiple of 4 bytes

/strequal:
    ldw $3, $1[-4]          # get the length of string 1
    ldw $4, $2[-4]          # get the length of string 2
    ld $8, 0               # set $8 to 0 - default return value
    bne $3, $4, .done      # if the lengths are not equal, we are done
    add $3, $1             # point to end of string 1  (Note since we know both strings are the same length,
                           # we don't need to calculate which is shorter
.loop:
    ldw $4, $1[0]          # get a word
    ldw $5, $2[0]          # get a word
    bne $4, $5, .done      # if the words are not equal, we are done
    add $1, 4              # increment string 1 pointer
    add $2, 4              # increment string 2 pointer
    blt $1, $3, .loop      # if we have not reached the end of the strings then loop
    ld $8, 1               # set $8 to 1 (true)
.done:
    ret

# ==========================================================
#                     memcpy
# ==========================================================
# $1 = pointer to destination
# $2 = pointer to source
# $3 = number of bytes to copy
/memcpy:
    beq $3, 0, .done        # if $3 is 0, there is nothing to do
    or $4, $1, $2           # look to see if both pointers are word-aligned
    and $4, 3               # if so we can copy in words
    ld $5, 4                # set $5 to 4
    beq $4, 0, .word_copy
    jmp .byte_copy

.word_copy_loop:
    ldw $4, $2[0]           # get a word
    stw $4, $1[0]           # store a word
    add $1, 4                # increment destination pointer
    add $2, 4                # increment source pointer
    sub $3, 4                # decrement count
 .word_copy:
    bge $3, $5, .word_copy_loop # if $3 is greater than or equal to 4, loop
    jmp .byte_copy

 .byte_copy_loop:
    ldb $4, $2[0]           # get a byte
    stb $4, $1[0]           # store a byte
    add $1, 1                # increment destination pointer
    add $2, 1                # increment source pointer
    sub $3, 1                # decrement count
 .byte_copy:
    bne $3, 0, .byte_copy_loop

.done:
    ret